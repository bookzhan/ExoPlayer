// Copyright (C) 2017 The Android Open Source Project
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

apply plugin: 'maven-publish'
apply from: "$gradle.ext.exoplayerSettingsDir/missing_aar_type_workaround.gradle"

ext {
    NEXUS_USER = ''
    NEXUS_PASSWORD = ''
    RELEASE_SNAPSHOT = false

    POM_URL_SNAP = 'https://www.raoyunsoft.com/nexus/repository/maven-snapshots/'
    POM_URL = 'https://www.raoyunsoft.com/nexus/repository/maven-releases/'
}

if (System.getenv('NEXUS_USER')) {
    NEXUS_USER = System.getenv('NEXUS_USER')
}
if (System.getenv('NEXUS_PASSWORD')) {
    NEXUS_PASSWORD = System.getenv('NEXUS_PASSWORD')
}
println("NEXUS_USER=" + NEXUS_USER + " NEXUS_PASSWORD=" + NEXUS_PASSWORD)

afterEvaluate {
    publishing {
        repositories {
            maven {
                url RELEASE_SNAPSHOT ? POM_URL_SNAP : POM_URL
                credentials {
                    username NEXUS_USER
                    password NEXUS_PASSWORD
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.release
                artifact androidSourcesJar
                groupId = 'com.guaishou.bzlib'
                artifactId = releaseArtifactId
                version = RELEASE_SNAPSHOT ? releaseVersion + "-SNAPSHOT" : releaseVersion
                pom {
                    name = releaseArtifactId
                    description = releaseDescription
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            name = 'The Android Open Source Project'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/google/ExoPlayer.git'
                        url = 'https://github.com/google/ExoPlayer'
                    }
//                    withXml {
//                        addMissingAarTypeToXml(it)
//                    }
                }
            }
        }
    }
}
tasks.withType(PublishToMavenRepository) { it.dependsOn lint }

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}
